
/*
	Platooning Scenario 3 FOLLOWER AGENT

	Recoverable latency problems. All vehicles have REs + leader RE. vehicles adapt spacing parameters based on latency problems
	An example of recoverable latency problem: if the difference between the time stamp on a message and the vehicle's clock is too great then the spacing should be double.
	In this scenario, we are interested to know what is the platoon tolerance to latency. How far we can get the platoon together. 
	How far can vehicles be from each other in order to still keep platoon. REs in vehicles independently decide about changing their spacing parameters and RE in leader decides 
	how spacing parameter can grow. For instance, if two vehicles are too-far, then the leader should not allow more increase of space between those two vehicles otherwise the platoon 
	breaks to parts that is described in Scenario 4.  

	For recoverable latency problem, we can verify that too-far state (defined for this particular control strategy) is not reachable.

*/


:name: follower

:Initial Beliefs:

:Initial Goals:



:Plans:

/* Default plans for handling messages */
+.received(:tell, B): {True} <- +B;   
+.received(:perform, G): {True} <- +!G [perform];
+.received(:achieve, G): {True} <- +!G [achieve];

//initialisation phase-- vehicle joins to platoon after 50 sec
+platoon_set_up: {True}<- perf(speed_controller(1)), perf(steering_controller(1));

// check for latency 0 and 1 to add goals to set spacing to 5 and 10, accordingly
+latency(0): {~G nominal_space [achieve], ~G double_nominal_space [achieve]}<- +!nominal_space [achieve];
+latency(0): {G double_nominal_space [achieve]}<- -!double_nominal_space [achieve], +!nominal_space [achieve];
+latency(1): {~G double_nominal_space [achieve], ~G nominal_space [achieve]}<- +!double_nominal_space [achieve];
+latency(1): {G nominal_space [achieve]}<- -!nominal_space [achieve], +!double_nominal_space [achieve];
+latency(K): {True}<- print(K);

// goal to set spacing to nominal (5)
+!nominal_space [achieve]: {True}<- perf(adjust_distance_parameter(0)), *nominal_space;

// goal to set spacing to double nominal (10)
+!double_nominal_space [achieve]: {True}<- perf(adjust_distance_parameter(1)), *double_nominal_space;

// if unrecoverable latency occurs, all goals are dropped and breaking_platoon action should be taken (assume it doesnt happen in this scenario)
+unrecoverable: {True}<- -!nominal_space [achieve], -!double_nominal_space [achieve], perf(breaking_platoon);
